#!/usr/bin/env zsh
# git-branch-purge
# --------------------------------------------------------------------------------------------------
# Version
# -------
# 1.0.0
# --------------------------------------------------------------------------------------------------
# Authors
# -------
# Filipe Kiss <hello@filipekiss.com.br> http://github.com/filipekiss
# --------------------------------------------------------------------------------------------------
# Usage
# -----
# Add this to your $PATH and invoke as a git command:
#
#   git branch-purge
#
# Or simply run ./git-branch-purge
# --------------------------------------------------------------------------------------------------
# Options
# -------
#
# --dry-run, -n
# Don't actually delete anything. Useful for checking what would be deleted
#
# --force, -f
# Force deletion of branches. Sometimes, the branches are identified as not
# being merged and git will refuse to delete them (this might happen in cases of
# squashed commits, for example). Using this options makes git ignore this and
# delete the branch anyway.
#
_parse_arguments() {
    local arg
    while (($# > 0)); do
        arg="$1"
        case "$arg" in
        --dry-run | -n)
            _dry_run="1"
            ;;
        --force|-f)
            _force_delete="-D"
            ;;
        --*)
            # Inexistent dashed option, do nothing
            ;;
        -*)
            # Inexistent dashed option, do nothing
            ;;
        "") ;;
        esac
        shift
    done
    return 0
}

_get_orphaned_branches() {
  git branch --format='%(if:equals=gone)%(upstream:track,nobracket)%(then)%(refname:short)%(else)===NO%(end)' | grep -v "===NO"
}


main() { 
  _parse_arguments "$@"
  echo "ğŸ•µï¸â€â™‚ï¸  Searching for branches that don't have a remote counterpartâ€¦"
  local _orphaned_branches=("${(@f)$(_get_orphaned_branches)}")
  for branch in "${_orphaned_branches[@]}"; do
    if [[ "$branch" == "" ]]; then
      echo "âœ¨ No branches to purge"
      exit 0
    fi
  if [[ -n ${_dry_run:-} ]]; then
      echo "ğŸª£ Would Delete: $branch"
  else
      echo "ğŸ§¹ Deleting: $branch"
      command git branch ${_force_delete:-"-d"} ${branch} > /dev/null
      if [[ $? -eq 0 ]]; then
        echo "ğŸ—‘ï¸  Deleted: $branch"
      fi
  fi
  done
}

main "$@"
